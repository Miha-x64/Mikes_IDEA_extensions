import java.nio.file.Files
import java.util.stream.Collectors

plugins {
    id "org.jetbrains.intellij" version "1.2.0"
    id 'org.jetbrains.kotlin.jvm' version "1.5.31"
}

def pluginVersion = '0.15'

group 'net.aquadc.mike'
version pluginVersion

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2020.3'
    plugins = ['java', 'Kotlin', 'android']
    updateSinceUntilBuild = true
}
compileJava {
    targetCompatibility = '11'
    sourceCompatibility = '11'
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
        languageVersion = "1.5"
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}
patchPluginXml {
    untilBuild = ''
    pluginDescription = Files.lines(project.file('README.md').toPath())
        .dropWhile(s -> !s.startsWith("#")).dropWhile(s -> s.startsWith("#"))
        .collect(Collectors.joining("\n")).trim().replace(
            '[Plugin page on JetBrains marketplace](https://plugins.jetbrains.com/plugin/12690-mike-s-idea-extensions)',
            '<a href="https://github.com/Miha-x64/Mikes_IDEA_extensions">Source code on GitHub</a>'
        )
    changeNotes = Files.lines(project.file('CHANGELOG.md').toPath())
            .dropWhile(s -> !s.startsWith("### $pluginVersion")).skip(1).takeWhile(s -> !s.startsWith("###"))
            .collect(Collectors.joining("\n")).trim()
}

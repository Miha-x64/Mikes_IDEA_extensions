import java.nio.file.Files
import java.util.stream.Collectors
import java.util.stream.Stream

plugins {
    id 'org.jetbrains.intellij' version '1.2.1'
    id 'org.jetbrains.kotlin.jvm' version "1.5.31"
}

def pluginVersion = '0.26'

group 'net.aquadc.mike'
version pluginVersion

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2021.3.3' // https://plugins.jetbrains.com/docs/intellij/android-studio-releases-list.html
    plugins = ['java', 'Kotlin', 'android']
    updateSinceUntilBuild = true
}
compileJava {
    targetCompatibility = '11'
    sourceCompatibility = '11'
}
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "11"
        languageVersion = "1.5"
        freeCompilerArgs += '-Xjvm-default=all'
    }
}
patchPluginXml {
    untilBuild = ''
    pluginDescription = Stream.concat(Files.lines(project.file('README.md').toPath())
            .dropWhile(s -> !s.startsWith("<!-- start plugin.xml -->")).skip(1).takeWhile(s -> !s.startsWith("<!-- end plugin.xml -->")),
            Stream.of("<a href=\"https://github.com/Miha-x64/Mikes_IDEA_extensions\">Source code on GitHub</a>"))
        .collect(Collectors.joining("\n")).trim()
    changeNotes = Files.lines(project.file('CHANGELOG.md').toPath())
            .dropWhile(s -> !s.startsWith("### $pluginVersion")).skip(1).takeWhile(s -> !s.startsWith("###"))
            .collect(Collectors.joining("\n")).trim()
}

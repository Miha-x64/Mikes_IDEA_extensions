import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import java.nio.file.Files
import java.util.stream.Collectors
import java.util.stream.Stream

plugins {
    id 'org.jetbrains.intellij' version '1.17.4'
    id 'org.jetbrains.kotlin.jvm' version "1.9.0"
}

def pluginVersion = '0.33'

group 'net.aquadc.mike'
version pluginVersion

repositories {
    mavenCentral()
}

dependencies {
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2022.3.1'//â€“2024.1 https://plugins.jetbrains.com/docs/intellij/android-studio-releases-list.html
    plugins = ['com.intellij.java', 'org.jetbrains.kotlin', 'android']
    updateSinceUntilBuild = true
}
compileJava {
    targetCompatibility = '17'
    sourceCompatibility = '17'
}
tasks.withType(KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "17"
        languageVersion = "1.7"
        freeCompilerArgs += '-Xjvm-default=all'
    }
}
patchPluginXml {
    untilBuild = ''
    pluginDescription = Stream.concat(Files.lines(project.file('README.md').toPath())
            .dropWhile(s -> !s.startsWith("<!-- start plugin.xml -->")).skip(1).takeWhile(s -> !s.startsWith("<!-- end plugin.xml -->")),
            Stream.of("<a href=\"https://github.com/Miha-x64/Mikes_IDEA_extensions\">Source code on GitHub</a>"))
        .collect(Collectors.joining("\n")).trim()
    changeNotes = Files.lines(project.file('CHANGELOG.md').toPath())
            .dropWhile(s -> !s.startsWith("### $pluginVersion")).skip(1).takeWhile(s -> !s.startsWith("###"))
            .collect(Collectors.joining("\n")).trim()
}
